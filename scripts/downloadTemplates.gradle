
//tag::downloadTemplate[]
task downloadTemplate(
    description: 'Download a specified template and install it',
    group: 'docToolchain helper'
) {
    doFirst {
    }
    doLast {
        def color = { color, text ->
            def colors = [black: 30, red: 31, green: 32, yellow: 33, blue: 34, magenta: 35, cyan: 36, white: 37]
            return new String((char) 27) + "[${colors[color]}m${text}" + new String((char) 27) + "[0m"
        }
        println "${color 'green', 'Install arc42 documentation template.'}"
        println "For more information about arc42 see https://arc42.org"
        def targetDir = new File(new File(docDir, inputPath), 'arc42')
        if (targetDir.exists()) {
            println "${targetDir.canonicalPath} already exists"
            println "in order to re-install the template, please remove the folder first and re-run the script"
            throw new RuntimeException("Template folder already exists")
        }
        def lang = ant.input(message: "${color 'green', 'Which language do you want to install?'}", validargs: 'EN,DE,ES,RU', addproperty: 'lang')
        def help = ant.input(message: "${color 'green', 'Do you want the template with or without help?'}", validargs: 'withhelp,plain', addproperty: 'help')
        targetDir.mkdirs()
        download {
            src 'https://github.com/arc42/arc42-template/raw/master/dist/arc42-template-' + ant.lang + '-' + ant.help + '-asciidoc.zip'
            dest new File(targetDir, 'arc42.zip')
        }
        copy {
            from zipTree(new File(targetDir, 'arc42.zip'))
            into targetDir
        }
        delete {
            delete new File(targetDir, 'arc42.zip')
        }
        println "arc42 template unpacked into ${targetDir}"
        //re-organize content
        //move images folder
        new File(targetDir, '../images/.').mkdirs()
        copy {
            from (new File(targetDir, 'images/.'))
            into new File(targetDir, '../images/.')
        }
        delete new File(targetDir, 'images/.')
        new File(targetDir, 'src').renameTo(new File(targetDir, 'chapters'))
        //add jbake header
        new File(targetDir, 'chapters').eachFileRecurse { file->
            if (file.name.endsWith('.adoc') && file.name ==~ /[0-9]+_.*/) {
                def text = file.getText('utf-8')
                def title = ""
                text.eachLine { line ->
                    if (title=="" && line.startsWith('=')) {
                        title = line.split(" ")[1..-1].join(" ")
                    }
                }
                text = """\
:jbake-title: $title
:jbake-type: page_toc
:jbake-status: published
:jbake-menu: arc42
:jbake-order: ${file.name.split("_")[0] as Integer}
:filename: ${file.canonicalPath-targetDir.canonicalPath}
ifndef::imagesdir[:imagesdir: ../../images]

:toc:

"""+text
                file.write(text,'utf-8')
            }
        }
        new File(targetDir, 'arc42-template.adoc').renameTo(new File(targetDir, 'arc42.adoc'))
        def configAdoc = new File(targetDir,'chapters/config.adoc')
        configAdoc.write(configAdoc.text.replaceAll("[.]/images","../images"))
        def mainDoc = new File(targetDir,'arc42.adoc')
        mainDoc.write(":imagesdir: ../images\n"+mainDoc.text.replaceAll('src/','chapters/'))
        //write asciidoctorconfig for editor preview
        new File(targetDir,'/chapters/.asciidoctorconfig.adoc').write(':imagesdir: ../../images\n\n')
        new File(targetDir,'/.asciidoctorconfig.adoc').write(':imagesdir: ../images\n\n')
        //add arc42.doc to config
        def configFile = new File(docDir, mainConfigFile)
        configFile.write(
            configFile.text
                .replaceAll('/[*]{2} inputFiles [*]{2}/',
                            "[file: 'arc42/arc42.adoc', formats: ['html','pdf']],\n\t/** inputFiles **/")
                .replaceAll('/[*]{2} imageDirs [*]{2}/',
                            "'images/.',\n\t/** imageDirs **/")
        )
        println "added template to ${mainConfigFile}"
        println "use 'generateHTML' or 'generatePDF' to convert the template"

    }
}
//end::downloadTemplate[]
